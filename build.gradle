import java.time.ZoneOffset
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.47.0"
    id 'jacoco'
}

group 'com.github.ram042'
version System.getenv("GITHUB_REF") != null && System.getenv("GITHUB_REF").startsWith("refs/tags/")
        ? System.getenv("GITHUB_REF").split("/").last()
        : ZonedDateTime.now(ZoneOffset.UTC).format(DateTimeFormatter.ofPattern("YYYY.MM.dd.HH.mm.ss")) + "-UNSTABLE"

java {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

test {
    useTestNG()
    jacoco {
        enabled = true
    }
    testLogging {
        exceptionFormat = 'full'
    }
    systemProperty 'org.slf4j.simpleLogger.logFile', 'System.out'
}

jacocoTestReport {
    dependsOn test
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.slf4j:slf4j-simple:2.0.7'
    testImplementation 'org.testng:testng:7.5.1'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ram042/json")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) { it.dependsOn test }

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
