import java.time.ZoneOffset
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.27.0"
    id 'jacoco'
    id "me.champeau.gradle.jmh" version "0.5.0"
}

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

test {
    useTestNG()
    jacoco {
        enabled = true
    }
    testLogging {
        exceptionFormat = 'full'
    }
}

jacocoTestReport {
    dependsOn test
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'org.slf4j:slf4j-simple:1.7.26'
    testImplementation 'org.testng:testng:7.1.0'

    jmh 'com.google.code.gson:gson:2.8.6'
    jmh 'com.googlecode.json-simple:json-simple:1.1.1'
    jmh 'org.json:json:20190722'
    jmh 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
}

tasks.getByName('jmhJar').doFirst() { duplicatesStrategy(DuplicatesStrategy.EXCLUDE) }
jmh {
    duplicateClassesStrategy = 'exclude'
    resultFormat = 'JSON'
    benchmarkMode = ["avgt", "sample", "ss"]
//    benchmarkMode = ["ss"]
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

group 'moe.orangelabs'
version System.getenv("GITHUB_REF") == null
        ? ZonedDateTime.now(ZoneOffset.UTC).format(DateTimeFormatter.ofPattern("YYYY.MM.dd.HH.mm.ss"))+"-nightly"
        : System.getenv("GITHUB_REF").split("/").last()

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            name = "PublicS3"
            System.setProperty("org.gradle.s3.endpoint", "https://maven.orangelabs.moe.storage.yandexcloud.net/")
            def releasesRepoUrl = "s3://releases"
            def snapshotsRepoUrl = "s3://snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(AwsCredentials) {
                accessKey System.getenv("AWS_ACCESS_KEY_ID")
                secretKey System.getenv("AWS_SECRET_ACCESS_KEY")
            }
        }
        maven {
            name = "Bintray"
            def releasesRepoUrl = "https://api.bintray.com/maven/orangelabs-moe/releases/${project.name}/;publish=1"
            def snapshotsRepoUrl = "https://api.bintray.com/maven/orangelabs-moe/snapshots/${project.name}/;publish=1"
            println "v" + getVersion()
            url = version.endsWith('nightly') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("BINTRAY_USERNAME")
                password System.getenv("BINTRAY_API_TOKEN")
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) { it.dependsOn test }

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
